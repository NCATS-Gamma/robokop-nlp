// context-free grammar for biomedical question parsing, in EBNF-ish form
// https://github.com/erezsh/lark/wiki/Grammar-Reference
//
// assume that text has been stripped of punctuation and converted to lowercase

sentence: nom vp -> declaration
    | WHAT nom vp Q -> q_what_0 // What genes affect Ebola?, What cells are affected by Ebola?
    | WHAT nom DO nom VB Q -> q_what_1 // What cells does Ebola affect?
    | WHAT nom BE nom VBD IN Q -> q_what_3 // What genes is Ebola affected by?
    | "what is the cop for" nom Q -> q_cop
    | "what genetic condition protects against" nom Q -> q_gc

nom: NOUN -> noun
    | "[" NOUN "]"
    | nom CC nom -> conjunction
    | nom THAT vp
// verb phrase
vp: VB nom
    | BE obj -> passive
    | vp pp
obj: VBD pp // passive
    | nom
pp: IN nom 

Q: "?"
WHAT: "what" | "which"
NOUN: /genes?/
    | /diseases?/
    | /drugs?/
    | /biological( |_)process(es)?/
    | /anatomical( |_)structures?/
    | /phenotypic( |_)features?/
    | /phenotypes?/
    | /chemical( |_)substances?/
    | /genetic( |_)conditions?/
    | /cells?/
    | /ENTITY[0-9]+/
VB: /affects?/
    | /interacts? with/
    | /regulates?/
    | /causes?/
    | /treats?/
    | /prevents?/
    | /express(es?)/
    | /produces?/
    | /(is|are)/
VBD: "affected"
    | "regulated"
    | "caused"
    | "treated"
    | "prevented"
    | "expressed"
    | "produced"
    | "associated"  // with
    | "related"   // to
    | "expressed" // in
JJIN: "associated with"
    | "related to"
    | "expressed in"
CC: "and" | "or"
IN: "by" | "to" | "with" | "in" | "of"
THAT: "that" | "which" // subordinating conjunction
DO: /do(es)?/
BE: /(is|are)/

%ignore "[" "]"
%import common.WS
%ignore WS